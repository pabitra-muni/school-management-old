group 'org.ahant:school-management-library'
version '1.1-SNAPSHOT'

apply plugin: 'eclipse'
apply plugin: 'idea'

allprojects{
    repositories {
        mavenCentral()
    }
    apply plugin: 'java'
    apply plugin: 'jacoco'
}

sourceCompatibility = 1.8

allprojects{ jacocoTestReport.dependsOn test }

subprojects {
    test {
        useTestNG() {
            parallel = "tests"
            useDefaultListeners = false
            listeners = ['org.uncommons.reportng.HTMLReporter']
            suiteXmlFiles = fileTree(dir: "${rootDir}/${project.name}/src/test/resources", include: "testng.xml") as List
        }
        testLogging.showStandardStreams = true
        ignoreFailures = false
        reports.junitXml.enabled = false
        reports.html.enabled = true
    }
}

test {
    testLogging.showStandardStreams = true
    ignoreFailures = false
    reports.junitXml.enabled = false

    // Integration tests are disabled, by default, as they fail in CI build.
    onlyIf { project.hasProperty("runIT") }
}

dependencies {
    compile project(':school-management-core')
    compile project(':school-management-auth')
    compile project(':school-management-admission')
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.springframework:spring-test:4.2.5.RELEASE'
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.0.1"
    }
}
apply plugin: "org.sonarqube"
